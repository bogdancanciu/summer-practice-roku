sub init()
    m.menu = m.top.findNode("menu")
    m.homeContainer = m.top.findNode("homeContainer")
    m.freaksContainer = m.top.findNode("freaksContainer")
    m.profileContainer = m.top.findNode("profileContainer")

    m.menu.observeField("itemFocused", "onItemFocusedChanged")
    m.menu.itemFocused = 0
end sub

sub onItemFocusedChanged(event as Object)
    itemFocused = event.getData()
    buttonIdentifier = m.menu.findNode("navigation").getChild(itemFocused).identifier

    if buttonIdentifier = "homeButton"
        showHomeContainer()
    else if buttonIdentifier = "freaksButton"
        getFreaksList()
    else if buttonIdentifier = "profileButton"
        showProfileContainer()
    end if
end sub

sub showHomeContainer()
    m.homeContainer.visible = true
    m.freaksContainer.visible = false
    m.profileContainer.visible = false
end sub

sub showFreaksContainer()
    m.homeContainer.visible = false
    m.freaksContainer.visible = true
    m.profileContainer.visible = false
end sub

sub showProfileContainer()
    m.homeContainer.visible = false
    m.freaksContainer.visible = false
    m.profileContainer.visible = true
end sub

sub getFreaksList()
    m.getFreaksTask = CreateObject("roSGNode","GetFreaksTask")
    m.getFreaksTask.observeField("responseContent", "onResponseReady")

    m.getFreaksTask.control = "RUN"
end sub

sub onResponseReady(event as Object)
    responseContent = event.getData()

    m.freaksContainer.content = responseContent
    showFreaksContainer()
end sub

function onKeyEvent(key as String, press as Boolean) as Boolean
    if press
        freaksGrid = m.freaksContainer.findNode("navigation")
        if key="right" and m.freaksContainer.visible = true
            freaksGrid.setFocus(true)
            m.freaksContainer.callFunc("setDetailsVisibility", true)
        else if key="left" and freaksGrid.hasFocus()
            mainMenu = m.menu.findNode("navigation")
            mainMenu.setFocus(true)
            m.freaksContainer.callFunc("setDetailsVisibility", false)
        end if
    end if

    return true
end function
